plugins {
    id 'info.solidsoft.pitest' version '1.9.0'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
}

group 'com.agonyforge'
version '0.0.4'

repositories {
    mavenCentral()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

configurations.maybeCreate("pitest")

dependencies {
    pitest 'org.pitest:pitest-junit5-plugin:1.1.0'

    implementation platform('software.amazon.awssdk:bom:2.18.15')
    implementation 'software.amazon.awssdk:dynamodb'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.0.1'
    testImplementation 'com.amazonaws:DynamoDBLocal:1.16.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

task copyNativeLibs(type: Copy) {
    from (configurations.testRuntimeClasspath) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

test {
    useJUnitPlatform()

    doFirst {
        systemProperty "java.library.path", 'build/libs'
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': "Agony Forge DynamoDB",
            'Implementation-Version': getArchiveVersion()
        )
    }
}

bootJar {
    enabled = false // don't build the spring boot jar
}

tasks.pitest {
    doFirst {
        systemProperty "java.library.path", 'build/libs'
    }
}

pitest {
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false

    targetClasses = [
        "com.agonyforge.mud.models.dynamodb.*"
    ]

    excludedMethods = [
        "hashCode",
        "equals"
    ]

    excludedClasses = [
        "com.agonyforge.mud.models.dynamodb.config.*",
        "com.agonyforge.mud.models.dynamodb.*.Abstract*"
    ]
}

test.dependsOn tasks.copyNativeLibs
tasks.pitest.mustRunAfter test
check.dependsOn tasks.pitest
