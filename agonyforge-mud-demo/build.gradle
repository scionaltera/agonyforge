plugins {
    id 'info.solidsoft.pitest' version '1.9.11'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.palantir.docker' version '0.35.0'
    id 'java'
}

group = 'com.agonyforge'
version = '0.0.10-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations.maybeCreate("pitest")

dependencyManagement {
    imports {
        mavenBom 'software.amazon.awssdk:bom:2.20.157'
    }
}

dependencies {
    pitest 'org.pitest:pitest-junit5-plugin:1.2.0'

    implementation project(":agonyforge-mud-core")
    implementation project(":agonyforge-mud-models-dynamodb")

    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.hazelcast:hazelcast:5.3.2'

    implementation 'org.webjars:webjars-locator:0.47'
    implementation 'org.webjars:bootstrap:5.3.2'
    implementation 'org.webjars:jquery:3.7.1'
    implementation 'org.webjars.npm:webstomp-client:1.2.6'
    implementation 'org.webjars.npm:sockjs-client:1.6.1'

    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.security:spring-security-oauth2-core'
    implementation 'org.springframework.session:spring-session-core'

    testImplementation(testFixtures(project(":agonyforge-mud-models-dynamodb")))

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false // don't build the non-repackaged jar
}

bootJar {
    manifest {
        attributes(
            'Implementation-Title': 'Agony Forge',
            'Implementation-Version': getArchiveVersion()
        )
    }
}

pitest {
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false

    targetClasses = [
        "com.agonyforge.mud.demo.*"
    ]

    excludedMethods = [
        "hashCode",
        "equals"
    ]

    excludedClasses = [
        "com.agonyforge.mud.demo.config.*"
    ]
}

docker {
    name "scionaltera/agonyforge-mud-demo"
    dockerfile file('src/main/docker/Dockerfile.local')
    files bootJar.outputs
}

build.dependsOn tasks.docker
tasks.pitest.mustRunAfter test
check.dependsOn tasks.pitest
