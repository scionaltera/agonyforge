plugins {
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.palantir.docker' version '0.36.0'
    id 'java'
}

group = 'com.agonyforge'
version = '0.0.12-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations.maybeCreate("pitest")

dependencies {
    pitest 'org.pitest:pitest-junit5-plugin:1.2.3'

    implementation project(":agonyforge-mud-core")

    implementation 'org.postgresql:postgresql:42.7.10'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.hazelcast:hazelcast:5.6.0'
    implementation 'ch.qos.logback.access:logback-access-tomcat:2.0.12'

    implementation 'org.webjars:webjars-locator:0.52'
    implementation 'org.webjars:bootstrap:5.3.8'
    implementation 'org.webjars:jquery:3.7.1'
    implementation 'org.webjars.npm:webstomp-client:1.2.6'
    implementation 'org.webjars.npm:sockjs-client:1.6.1'

    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false // don't build the non-repackaged jar
}

bootJar {
    manifest {
        attributes(
            'Implementation-Title': 'Agony Forge',
            'Implementation-Version': getArchiveVersion()
        )
    }
}

pitest {
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false
    useClasspathFile = true

    targetClasses = [
        "com.agonyforge.mud.demo.*"
    ]

    excludedMethods = [
        "hashCode",
        "equals"
    ]

    excludedClasses = [
        "com.agonyforge.mud.demo.config.*"
    ]

    jvmArgs.add("-XX:+EnableDynamicAgentLoading")
}

docker {
    name "scionaltera/agonyforge-mud-demo"
    dockerfile file('src/main/docker/Dockerfile.local')
    files bootJar.outputs
}

build.dependsOn tasks.docker
tasks.pitest.mustRunAfter test
check.dependsOn tasks.pitest
